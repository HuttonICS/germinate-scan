apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            // This will be filled with our top secret data...
        }
    }

    dataBinding {
        enabled = true
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "uk.ac.hutton.android.germinatescan"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 37
        versionName "1.19.04.02"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            // Mark the debug version
            applicationIdSuffix '.debug'
            versionNameSuffix ' Debug'
        }
    }
}

repositories {
    mavenCentral()

    maven { url "http://dl.bintray.com/journeyapps/maven" }
    maven { url "https://jitpack.io" }
    maven { url "https://maven.google.com" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-analytics:16.0.4'
    implementation 'com.heinrichreimersoftware:material-intro:1.6'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.andkulikov:transitionseverywhere:1.7.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    // Supports Android 4.0.3 and later (API level 15)
    implementation 'com.journeyapps:zxing-android-embedded:3.2.0@aar'
    implementation 'com.google.zxing:core:3.2.0'
    implementation 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'
    implementation 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}


// Get the key store information from the properties file
def props = new Properties()
props.load(new FileInputStream(project.file("release.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword